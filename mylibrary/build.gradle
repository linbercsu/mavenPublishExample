apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}


task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "source"
}

publishing {
    publications {
        bar(MavenPublication) {
            groupId 'com.foo'
            artifactId 'bar'
            version '0.3-SNAPSHOT'
            artifact(sourceJar)
            artifact("$buildDir/outputs/aar/mylibrary-debug.aar")

            pom.withXml {
                def node = node()
                node.appendNode('description',
                        'A demonstration of maven POM customization')
                def de = node.appendNode('dependencies').appendNode('dependency')
                de.appendNode('groupId', 'com.android.support')
                de.appendNode('artifactId', 'appcompat-v7')
                de.appendNode('version', '25.3.1')
            }
        }
    }
    repositories {
        maven {
            name 'Dev'
            credentials {
                username 'admin'
                password 'admin123'
            }

            // change to point to your repo, e.g. http://my.org/repo
            url "http://10.7.0.217:8082/nexus/content/repositories/snapshots/"

        }
    }
}


dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:25.3.1'
    testCompile 'junit:junit:4.12'
}
